# Maintainer: begin-theadventure <begin-thecontact.ncncb at dralias dot com>

pkgname=kana-gtk4
pkgver=1.2
pkgrel=1
pkgdesc="Learn Japanese characters"
url="https://gitlab.gnome.org/fkinoshita/kana"
arch=('x86_64' 'aarch64')
license=('GPL3')
depends=('gst-plugins-bad-libs' 'libadwaita')
makedepends=('cargo' 'git' 'meson')
checkdepends=('appstream-glib')
source=("git+$url.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  cd kana
  export CARGO_HOME="$srcdir/CARGO_HOME"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  export CARGO_HOME="$srcdir/CARGO_HOME"
  export RUSTUP_TOOLCHAIN=stable
  arch-meson kana build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
