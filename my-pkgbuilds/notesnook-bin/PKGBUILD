# Maintainer: begin-theadventure <begin-thecontact.ncncb at dralias dot com>

_pkgname=notesnook
pkgname=$_pkgname-bin
pkgdesc="A fully open source & end-to-end encrypted note taking alternative to Evernote (binary release)"
pkgver=2.5.0
pkgrel=4
arch=('x86_64' 'arm64')
url="https://github.com/streetwriters/notesnook"
license=('GPL3')
depends=('libappindicator-gtk3' 'libnotify' 'libxss' 'libxtst')
makedepends=('fuse2')
conflicts=($_pkgname)
provides=($_pkgname)
_appimage="${_pkgname}_${pkgver}_linux_x86_64.AppImage"
source_x86_64=("$_appimage::$url/releases/download/v$pkgver/${_pkgname}_linux_x86_64.AppImage")
source_arm64=("$_appimage::$url/releases/download/v$pkgver/${_pkgname}_linux_arm64.AppImage")
sha256sums_x86_64=('2e93cf8031e0b8189986002911f2d0f8959975c2e2388403814307ebc9567321')
sha256sums_arm64=('419775251c4b5af4239f793ed64d11b9bfda0e1327ae3d9c6d9294f57ff8bb74')

_fix_permissions() (
  target=$1

  if [[ -L "$target" ]]; then
    return 0
  fi

  if [[ -d "$target" || -x "$target" ]]; then
    chmod 755 "$target"
    return 0
  fi

  if [[ -f "$target" ]]; then
    chmod 644 "$target"
    return 0
  fi

  echo "Unrecognizable filesystem entry: $target" >&2
  return 1
)

prepare() {
  # Extract the AppImage
  chmod +x "./$_appimage"
  "./$_appimage" --appimage-extract
  chmod 755 squashfs-root
  # Edit the shortcut
  cd squashfs-root
  sed -i -E "s|Exec=AppRun|Exec=$_pkgname|g" $_pkgname.desktop
}

package() {
  # Create folders
  mkdir -p "$pkgdir/opt/$_pkgname" "$pkgdir/usr/bin" "$pkgdir/usr/share"
  # Install
  cd squashfs-root
  mv usr/share/icons "$pkgdir/usr/share"
  install -Dm644 $_pkgname.desktop -t "$pkgdir/usr/share/applications"
  ln -s /opt/$_pkgname/$_pkgname "$pkgdir/usr/bin/$_pkgname"
  rm -dr usr & rm ${_pkgname}.png .DirIcon ${_pkgname}.desktop
  cp -r * "$pkgdir/opt/$_pkgname"
  # Fix permissions
  find "$pkgdir" | while read -r target; do
    _fix_permissions "$target"
  done
}
