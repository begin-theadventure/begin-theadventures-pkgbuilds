# Maintainer: begin-theadventure <begin-thecontact.ncncb at dralias dot com>

_pkgname=notesnook
pkgname=$_pkgname-bin
pkgdesc="Open source zero knowledge private note taking (binary release)"
pkgver=2.4.11
pkgrel=1
arch=('x86_64')
url="https://github.com/streetwriters/notesnook"
license=('GPL')
makedepends=('fuse2')
conflicts=($_pkgname)
provides=($_pkgname)
depends=('glibc' 'hicolor-icon-theme' 'zlib')
_appimage="${_pkgname}_${pkgver}_${pkgrel}_linux_x86_64.AppImage"
source=("$_appimage::$url/releases/download/v$pkgver/${_pkgname}_linux_x86_64.AppImage")
sha256sums=('b6e241b6f12ea47ce3bebc988e47e0188b0b358ce191db4932acfe765ffe1f15')

_fix_permissions() (
  target=$1

  if [[ -L "$target" ]]; then
    return 0
  fi

  if [[ -d "$target" || -x "$target" ]]; then
    chmod 755 "$target"
    return 0
  fi

  if [[ -f "$target" ]]; then
    chmod 644 "$target"
    return 0
  fi

  echo "Unrecognizable filesystem entry: $target" >&2
  return 1
)

prepare() {
  # Extract the AppImage
  chmod +x "./$_appimage"
  "./$_appimage" --appimage-extract
  chmod 755 squashfs-root
  # Edit the shortcut
  cd squashfs-root
  sed -i -E "s|Exec=AppRun|Exec=$_pkgname|g" "${srcdir}/squashfs-root/$_pkgname.desktop"
}

package() {
  # Create folders
  mkdir -p $pkgdir/opt
  mkdir -p $pkgdir/usr/bin
  mkdir -p $pkgdir/usr/share/applications
  mkdir -p $pkgdir/usr/share/icons/hicolor
  # Install
  mv squashfs-root/usr/share/icons/hicolor "$pkgdir/usr/share/icons"
  cp -r squashfs-root "$pkgdir/opt/Notesnook"
  ln -s /opt/Notesnook/$_pkgname $pkgdir/usr/bin/$_pkgname
  ln -s /opt/Notesnook/$_pkgname.desktop "$pkgdir/usr/share/applications/$_pkgname.desktop"
  ln -fs /usr/share/icons/hicolor/1024x1024/apps/$_pkgname.png "$pkgdir/opt/Notesnook/.DirIcon"
  ln -fs /usr/share/icons/hicolor/1024x1024/apps/$_pkgname.png "$pkgdir/opt/Notesnook"
  # Fix permissions
  find "$pkgdir" | while read -r target; do
    _fix_permissions "$target"
  done
}
